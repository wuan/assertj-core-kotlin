buildscript {
    ext.kotlin_version = '1.0.4'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.6.3'
//    id 'findbugs'
}

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'

archivesBaseName = 'assertj-core-kotlin'
group = 'net.wuerl.kotlin'
version = '0.1.3'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.assertj:assertj-core:2.5.0'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = false
    }
}

jacoco {
    toolVersion = '0.7.6.201602180812'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

if (!hasProperty('sonatypeUsername')) {
    ext.sonatypeUsername = ''
}
if (!hasProperty('sonatypePassword')) {
    ext.sonatypePassword = ''
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'assertj-core-kotlin'
                packaging 'jar'
                description 'assertj-core-kotlin is a thin wrapper to use AssertJ Core from Kotlin'
                url 'http://github.com/wuan/assertj-core-kotlin/'

                scm {
                    url 'scm:git@github.com:wuan/assertj-core-kotlin.git'
                    connection 'scm:git@github.com:wuan/assertj-core-kotlin.git'
                    developerConnection 'scm:git@github.com:wuan/assertj-core-kotlin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'wuan'
                        name 'Andreas WÃ¼rl'
                    }
                }
            }
        }
    }
}
